kill(all);

assume(dx>0);
h(n):= block([q, body: sum(a[i]*x^i, i, 0, n)], subst(body,  'q, lambda([x], q)));
f(n):= block([q, body: 1/dx*integrate(h(n)(dzita), dzita, -dx/2+x, dx/2+x)], subst(body, 'q, lambda([x], q)));

S[0]: [-2, -1, 0];
S[1]: [-1, 0,  1];
S[2]: [0,  1,  2];
S[6]: [-2, -1, 0, 1, 2, 3];
klist: flatten(rest(arrayinfo(S), 2));

for k in klist do (
  n: length(S[k])-1,
  eq: makelist ( (f(n))(q*dx) = f[i+q], q, S[k]),
  avar: delete(x, listofvars(h(n))),
  so: solve(eq, avar)[1],
  fr[k]: subst(so, f(n)),
  fwave[k]: subst(so, h(n)));

factor(expand(fwave[0](1/2*dx)));
factor(expand(fwave[1](1/2*dx)));
factor(expand(fwave[2](1/2*dx)));

/* eq. (16) */
factor(expand(fwave[6](1/2*dx)));

load("scheme2t.mac");
expand(scheme2t(factor(expand(fwave[0](dx/2))), i, 4));

for k in klist do (
  be[k]:   expand(sum(dx^(2*j-1)*integrate(diff(fwave[k](x), x, j)^2,
        x, -dx/2, dx/2),
      j, 1, length(S[k]))));

expand(be[0]);

beta_paper[0]: 1/4*(f[i-2] - 4*f[i-1] + 3*f[i])^2 + 13/12*(f[i-2] - 2*f[i-1] + f[i])^2;
beta_paper[1]: 1/4*(f[i-1] - f[i+1])^2 + 13/12*(f[i-1] - 2*f[i] + f[i+1])^2;
beta_paper[2]: 1/4*(3*f[i] - 4*f[i+1] + f[i+2])^2 + 13/12*(f[i] - 2*f[i+1] + f[i+2])^2;

expand(beta_paper[0] - be[0]);
expand(beta_paper[1] - be[1]);
expand(beta_paper[2] - be[2]);

taylor(expand(scheme2t(be[0], i, 6)), dx, 0, 5);
taylor(expand(scheme2t(be[1], i, 6)), dx, 0, 5);
taylor(expand(scheme2t(be[2], i, 6)), dx, 0, 5);

/* eq. (18) */
expand(1/dx*scheme2t(fwave[6](dx/2) - subst(i-1, i, fwave[6](dx/2)), i, 9));

d[0]: 1/10;
d[1]: 3/5;
d[2]: 3/10;

expand(lsum(scheme2t ( d[k]*(fr[k](1/2*dx) - fr[k](-1/2*dx))/dx, i, 6), k, [0, 1, 2]));

expand(taylor(scheme2t(fwave[0](dx/2), i, 6), dx, 0, 3));
expand(taylor(scheme2t(fwave[1](dx/2), i, 6), dx, 0, 3));
expand(taylor(scheme2t(fwave[2](dx/2), i, 6), dx, 0, 3));

expr: expand(taylor((scheme2t(lsum(d[k]*fwave[k](-dx/2), k, [0, 1, 2]), i, 6)), dx, 0, 5));
/*
eq: [
  ratcoef(expr, dx^3),
  ratcoef(expr, dx^2),
  ratcoef(expr, dx),
  ratcoef(expr, 1)];
eq: radcan(eq);  
solve(eq, [d[0], d[1], d[2]]);
*/

for k in [0, 1, 2] do (
  al[k]: d[k]/(be[k]+eps)^1
  );

for k in [0, 1, 2] do (
  om[k]: al[k]/lsum(al[k], k, [0, 1, 2])
  );

expr1: lsum(om[k]*fwave[k](dx/2), k, [0, 1, 2])$
expr2: subst(i-1, i, expr1)$

expr: expand(scheme2t(expr1 - expr2, i, 4))$
taylor(subst(eps=0, expr/dx), dx, 0, 5);

/* eq. (27) */
expand(1/6*scheme2t(be[0] + be[2] + 4*be[1], i, 3));
expand(scheme2t(be[6], i, 2));

expr: factor(12*be[6]);

block([display2d: false],
  for k: -2 thru 3 do
  for m: k thru 3 do
  print(ratcoef(part(expr, 1), f[i+k]*f[i+m])*f[i+k]*f[i+m]));